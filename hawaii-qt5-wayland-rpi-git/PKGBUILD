# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

buildarch=20

pkgname=hawaii-qt5-wayland-rpi-git
pkgver=20150318.318f9fc
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtWayland)"
arch=('armv6h' 'armv7h')
url="http://www.qt.io/"
license=('GPL3' 'LGPL')
depends=('qt5-base' 'qt5-declarative' 'libxcomposite' 'libxrender' 'mtdev' 'wayland' 'raspberrypi-firmware-tools')
provides=('qt5-wayland' 'hawaii-qt5-wayland-git')
conflicts=('qt5-wayland' 'hawaii-qt5-wayland-git')
replaces=('qt5-wayland' 'hawaii-qt5-wayland-git')
makedepends=('git' 'gdb')
options=('!libtool' 'debug')
source=('git://github.com/maui-packages/qtwayland#branch=output54'
        '0001-Fix-build-on-general-purpose-distros.patch'
        '0001-brcm-egl-Disambiguate-wl_registry.patch'
        '0001-Fix-build-for-brcm-egl-integration.patch'
        '0001-brcm-egl-Fix-build-of-hardware-integration.patch'
        '0001-brcm-egl-Avoid-conflicts-with-GLES3.patch')
md5sums=('SKIP'
         '053044c66d5cf7a1fe1aeb17387977f4'
         'd4fae1a1f8a51bb2ee4a38cdc6afd635'
         'c1b0043d80697a1005ef9e15ffd788c0'
         'b4671b4aa85fcd46b05fe64c044bf843'
         '17bbee0eeab0096f725daa0b887650c4')
_ver=5.6.0

pkgver() {
  cd qtwayland
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd qtwayland
  patch -p1 < $srcdir/0001-Fix-build-on-general-purpose-distros.patch
  patch -p1 < $srcdir/0001-brcm-egl-Disambiguate-wl_registry.patch
  patch -p1 < $srcdir/0001-Fix-build-for-brcm-egl-integration.patch
  patch -p1 < $srcdir/0001-brcm-egl-Fix-build-of-hardware-integration.patch
  patch -p1 < $srcdir/0001-brcm-egl-Avoid-conflicts-with-GLES3.patch
}

build() {
  cd qtwayland
  qmake CONFIG+=wayland-compositor DEFINES+=QT_NO_OPENGL_ES_3
  make
}

package() {
  cd qtwayland
  export CFLAGS=-DQT_NO_OPENGL_ES_3
  make INSTALL_ROOT="${pkgdir}" install

  # Workaround to install generated private headers
  cp ./include/QtCompositor/${_ver}/QtCompositor/private/{qwayland-server-*,*protocol*}.h \
      ${pkgdir}/usr/include/qt/QtCompositor/${_ver}/QtCompositor/private/
}
